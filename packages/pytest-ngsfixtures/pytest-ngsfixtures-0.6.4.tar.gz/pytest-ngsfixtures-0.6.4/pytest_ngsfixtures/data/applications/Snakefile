# -*- snakemake -*-
SAMPLES = ["CHS.HG00512", "CHS.HG00513", "PUR.HG00731", "PUR.HG00733",
           "YRI.NA19238", "YRI.NA19239"]

YUGEBAMFILES = expand("{sample}.yuge.sort.bam", sample=SAMPLES)
TINYBAMFILES = expand("{sample}.bam", sample=SAMPLES[2:4])

##############################
# Prerequisite temporary files
#
# - large bams for generating result files for parsing
##############################
rule link_ref:
    input: "../ref/scaffoldsN.fa"
    output: "ref.fa"
    shell: "ln -s {input} {output}"

rule bwa_index:
    input: "{prefix}.fa"
    output: "{prefix}.fa.sa"
    shell: "bwa index {input}"

rule bwa_yuge:
    input: ref = "ref.fa",
           read1="../yuge/{prefix}_1.fastq.gz",
           read2="../yuge/{prefix}_2.fastq.gz",
           index = "ref.fa.sa"
    output: "{prefix}.yuge.bam"
    log: "{prefix}.yuge.bam.log"
    shell: "bwa mem -H '@HD\tyuge test data' -R '@RG\tID:{wildcards.prefix}\tSM:{wildcards.prefix}' {input.ref} {input.read1} {input.read2} 2>{log} | samtools view -Sb - > {output}"

rule samtools_index:
    input: "{prefix}.bam"
    output: "{prefix}.bam.bai"
    shell: "samtools index {input}"

rule samtools_sort:
    input: "{prefix}.bam"
    output: "{prefix}.sort.bam"
    shell: "samtools sort {input} -o {output}"

rule samtools_sortn:
    input: "{prefix}.bam"
    output: "{prefix}.sortn.bam"
    shell: "samtools sort -n {input} -o {output}"

rule yuge_test_bam:
    input: "yuge.bam.fofn"
    output: "yuge.test.bam"
    shell: "samtools merge -f {output} -b {input}"

rule yuge_bam_fofn:
    input: YUGEBAMFILES
    output: "yuge.bam.fofn"
    shell: "ls -1 {input} > {output}"

# Small bam file output
rule bwa_mem:
    input: ref = "ref.fa",
           read1="../tiny/{prefix}_1.fastq.gz",
           read2="../tiny/{prefix}_2.fastq.gz",
           index = "ref.fa.sa"
    output: "{prefix}.bam"
    log: "{prefix}.bam.log"
    shell: "bwa mem -H '@HD\ttiny test data' -R '@RG\tID:{wildcards.prefix}\tSM:{wildcards.prefix}' {input.ref} {input.read1} {input.read2} 2>{log} | samtools view -Sb - > {output}"

rule tiny_bamfiles:
    input: TINYBAMFILES

##############################
# File types
##############################


rule clean:
    shell:
        "rm -f *.yuge.bam; "
        "rm -f *.yuge.bai; "
        "rm -f  *.bam.log; "
        "rm -f ref*; "
